apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    signingConfigs {
        release {
            keyAlias project.hasProperty('zlog_key_alias') ? zlog_key_alias : 'dummy'
            keyPassword project.hasProperty('zlog_key_password') ? zlog_key_password : 'dummy'
            storeFile file('../keystore.jks').exists() ? file('../keystore.jks') : null
            storePassword project.hasProperty('zlog_store_password') ? zlog_store_password : 'dummy'
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 5
        versionName '1.5'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = 'eu.boiled.zlog'
        pom.artifactId = 'zlog'
        pom.version = android.defaultConfig.versionName
        repository(url: 'file:./build/outputs')
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}